include "./api.poem"

var player =
  :x = 8
  :y = 8
  :dx = 1
  :dy = 0
  :length = 16
  :hist = array
var black = "!!!!"
var white = "æøå"
var food = "æ!~"

func new_game
  player:x = displayWidth/2
  player:y = displayHeight/2
  player:length = 1
  place_food

func place_food
  var x = random displayWidth
  var y = random displayHeight
  pset x y food

export "init" init
  setDisplayMode 1 displayWidth displayHeight
  setStepInterval 64
  focusInput 3
  pset 1 1 white
  white = pget 1 1
  pset 1 1 food
  food = pget 1 1
  new_game

export "step" step t
  array_push player:hist
    :x = player:x
    :y = player:y
  var i = 2
  while i
    if (array_length player:hist) > player:length
      var old = array_shift player:hist
      if food != pget old:x old:y
        pset old:x old:y black
    i -= 1
  var gameX = getGameAxisX
  var gameY = getGameAxisY
  if gameX > (abs gameY) && player:dy
    player:dx = 1
    player:dy = 0
  if gameX < (-abs gameY) && player:dy
    player:dx = -1
    player:dy = 0
  if gameY > (abs gameX) && player:dx
    player:dx = 0
    player:dy = 1
  if gameY < (-abs gameX) && player:dx
    player:dx = 0
    player:dy = -1
  player:x += player:dx
  player:y += player:dy
  if player:x < 0
    player:x += displayWidth
  if player:y < 0
    player:y += displayHeight
  if player:x >= displayWidth
    player:x -= displayWidth
  if player:y >= displayHeight
    player:y -= displayHeight
  if food == pget player:x player:y
    player:length += 2
    place_food
  if white == pget player:x player:y
    player:length = 1
  pset player:x player:y white
  ~ log2Numbers player:x player:y

export "display" render t
  displayMemory


